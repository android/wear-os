/*
 * Copyright (C) 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        // SDK and tools
        activityVersion = "1.4.0"
        composeVersion = "1.2.0-alpha02"
        composeCompilerVersion = "1.2.0-alpha02"
        coroutinesVersion = "1.6.0"
        kotlinVersion = "1.6.10"
        playServicesAuthVersion = "20.1.0"
        wearComposeVersion = "1.0.0-alpha15"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath libs.android.build.gradle
        classpath libs.kotlin.gradle.plugin
    }
}

plugins {
    id "com.diffplug.spotless" version "6.0.1"
    id "com.github.ben-manes.versions" version "0.41.0"
    id "nl.littlerobots.version-catalog-update" version "0.3.0"
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    apply plugin: "com.diffplug.spotless"

    spotless {
        kotlin {
            target "**/*.kt"
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ktlint("0.41.0")
            licenseHeaderFile rootProject.file("spotless/copyright.kt")
        }
    }
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        (it.candidate.version ==~ /.*-alpha.*/ && !(it.currentVersion ==~ /.*-alpha.*/)) ||
                (it.candidate.version ==~ /.*-beta.*/ && !(it.currentVersion ==~ /.*-(beta|alpha).*/)) ||
                (it.candidate.version ==~ /.*1.6.20-M.*/)
    }
}

versionCatalogUpdate {
    keep {
        keepUnusedVersions = true
    }
}
